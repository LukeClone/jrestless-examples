def checkstyleCfg = "${projectDir}/../../alien/checkstyle/checkstyle.xml" as File

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '0.0.1-SNAPSHOT'

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	/*
	 * Since we target Java 1.8, we cannot use Spring 3 but have to use Spring 4. Else we would see:  
	 * "ASM ClassReader failed to parse class file - probably due to a new Java class file version that isn't supported yet"
	 */ 
	compile('org.glassfish.jersey.ext:jersey-spring3:2.23') {
		exclude module: 'org.springframework'
	}
	compile (
		'org.springframework:spring-core:4.3.2.RELEASE',
		'org.springframework:spring-beans:4.3.2.RELEASE',
		'org.springframework:spring-web:4.3.2.RELEASE'	
	)
	
	/*
	 * Spring requires this and we have it as provided dependency, only.
	 * So we add it as compile time dependency since we don't have a Servlet container providing this for us.
	 * This, however, won't turn jrestless in a Servlet container.
	 * => you are not able to use/inject HttpServletRequest, ... 
	 */
	compile(
		'javax.servlet:javax.servlet-api:3.0.1'	
	)
	
	compile(
		'com.jrestless.aws:jrestless-aws-gateway-handler:0.2.0',
		'com.amazonaws:aws-lambda-java-log4j:1.0.0',
		'org.glassfish.jersey.media:jersey-media-json-jackson:2.23',
		'org.slf4j:slf4j-log4j12:1.7.21',
		'org.slf4j:jul-to-slf4j:1.7.21'
	)
	testCompile (
		'junit:junit:4.12',
		'org.mockito:mockito-core:1.10.19'
	)
}

checkstyle {
	configFile = checkstyleCfg
	toolVersion = '6.19'
	ignoreFailures = false
	sourceSets = [project.sourceSets.main]
}
build.dependsOn checkstyleMain

findbugs {
	toolVersion = '3.0.1'
	sourceSets = [project.sourceSets.main]
	effort = 'max'
}
tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

pmd {
	toolVersion = '5.4.2'
	sourceSets = [project.sourceSets.main]
}

task buildZip(type: Zip) {
	from compileJava
	from processResources
	into('lib') {
		from configurations.runtime
	}
}

build.dependsOn buildZip

task wrapper(type: Wrapper) {
	gradleVersion = '3.0'
}
